;;; an ASCII implementation of Conway's Game of Life

fn random_x() -> x {
    ' ' -> x;
    if(random(1) = 1) {
        'X' -> x;
    }
}

fn initialize(size) -> representation {

    [] -> row;
    [] -> representation;
    0 -> outer;
    while (outer < size) {
        0 -> inner;
        [] -> row;
        while(inner < size) {
            random_x() -> $row;
            inner + 1 -> inner;
        }
        row -> $representation;
        outer + 1 -> outer;
    }
}

fn print(representation) {
    for row in representation {
        prn row;
    }
}

;;; takes the current cell value and number of alive
;;; and generates what the outcome should be.
fn transition(current, alive) -> result {
    ' ' -> result;
    if((current = 'X') && alive < 2) {
        ' ' -> result;
    } elseif((current = 'X') && ((alive = 2) || (alive = 3))) {
        'X' -> result;
    } elseif((current = 'X') && alive > 3) {
        ' ' -> result;
    } elseif((current = ' ') && alive = 3) {
        'X' -> result;
    }
}

fn update(representation, size) -> representation {
    0 -> r;
    [] -> row;
    [] -> new_rep;
    for row in representation {
        0 -> c;
        [] -> new_row;

        ;;; the 'next' or below row
        [] -> below_row;
        if(r >= size - 1) {
            representation:0 -> below_row;
        } else {
            representation:(r + 1) -> below_row;
        }

        ;;; the 'preceding' or above row
        [] -> above_row;
        if(r = 0) {
            representation:(size - 1) -> above_row;
        } else {
            representation:(r - 1) -> above_row;
        }

        while(c < size) {
            0 -> alive;
            ;;; cell plus one
            if(c < size - 1) {
                if((row:(c + 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((row:0) = 'X') {
                1 + alive -> alive;
            }
            ;;; cell minus one
            if(c > 0) {
                if((row:(c - 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((row:(size - 1)) = 'X') {
                1 + alive -> alive;
            }
            ;;; below
            if((below_row:c) = 'X') {
                1 + alive -> alive;
            }

            ;;; above
            if((above_row:c) = 'X') {
                1 + alive -> alive;
            }

            ;;; below-right
            if(c < size - 1) {
                if((below_row:(c + 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((below_row:0) = 'X') {
                1 + alive -> alive;
            }

            ;;; below-left
            if(c > 0) {
                if((below_row:(c - 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((below_row:(size - 1)) = 'X') {
                1 + alive -> alive;
            }

            ;;; above-right
            if(c < size - 1) {
                if((above_row:(c + 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((above_row:0) = 'X') { 
                1 + alive -> alive;
            }

            ;;; above-left
            if(c > 0) {
                if((above_row:(c - 1)) = 'X') {
                    1 + alive -> alive;
                }
            } elseif((above_row:(size - 1)) = 'X') {
                1 + alive -> alive;
            }
            ;;; figure out new state
            transition(row:c, alive) -> $new_row;
            c + 1 -> c;
        }
        new_row -> $new_rep;
        r + 1 -> r;
    }
    new_rep -> representation;
}

start {
    arg(0) -> size;
    initialize(size) -> representation;
    print(representation);
    while(true) {
        update(representation, size) -> representation;
        ansi_up size;
        print(representation);
    }
}